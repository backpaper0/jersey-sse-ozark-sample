ext {
    javaeeVersion = '7.0'
    ozarkVersion = '1.0.0-m02'
    jerseyVersion = '2.22.2'
    superagentVersion = '1.4.0'
    knockoutVersion = '3.4.0'
    payaraVersion = '4.1.1.161'
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    providedCompile "javax:javaee-api:${javaeeVersion}"
    compile "org.glassfish.ozark:ozark:${ozarkVersion}"
    providedCompile "org.glassfish.jersey.media:jersey-media-sse:${jerseyVersion}"
    compile "org.webjars:superagent:${superagentVersion}"
    compile "org.webjars:knockout:${knockoutVersion}"
    archives "fish.payara.extras:payara-micro:${payaraVersion}"
}

war.archiveName = 'sample.war'

task run(type:Exec) {
    def payaraJar = configurations.archives.find { it.name == "payara-micro-${payaraVersion}.jar" }
    def warFile = war.archivePath
    commandLine 'java', '-jar', "$payaraJar", '--noCluster', '--deploy', "$warFile"
}

run.dependsOn war
